// Top-level build file where you can add configuration options common to all
// sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    google()
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

configurations {
    playstoreImplementation
    compatImplementation
    skrivaFreeCompatImplementation
}

ext {
    supportLibVersion = '28.0.0'
}

dependencies {
    playstoreImplementation('com.google.firebase:firebase-messaging:19.0.1') {
        exclude group: 'com.google.firebase', module: 'firebase-core'
        exclude group: 'com.google.firebase', module: 'firebase-analytics'
        exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
    }
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.+'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.exifinterface:exifinterface:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.emoji:emoji:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    compatImplementation 'androidx.emoji:emoji-appcompat:1.0.0'
    skrivaFreeCompatImplementation 'androidx.emoji:emoji-bundled:1.0.0'
    implementation 'org.bouncycastle:bcmail-jdk15on:1.61'
    implementation 'com.google.zxing:core:3.3.3'
    implementation 'de.measite.minidns:minidns-hla:0.2.4'
    implementation 'me.leolin:ShortcutBadger:1.1.22@aar'
    implementation 'org.whispersystems:signal-protocol-java:2.7.1'
    implementation 'com.makeramen:roundedimageview:2.3.0'
    implementation 'com.wefika:flowlayout:0.4.1'
    implementation 'net.ypresto.androidtranscoder:android-transcoder:0.3.0'
    implementation project(':libs:xmpp-addr')
    implementation 'org.osmdroid:osmdroid-android:6.1.0'
    implementation 'org.hsluv:hsluv:0.2'
    implementation 'org.conscrypt:conscrypt-android:2.1.0'
    implementation 'me.drakeet.support:toastcompat:1.1.0'
    implementation 'com.leinardi.android:speed-dial:2.0.1'
    implementation 'com.vanniktech:emoji-google:0.6.0'
    implementation 'com.google.guava:guava:28.0-android'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.17'
    implementation 'com.facebook.fresco:fresco:2.0.0'
    implementation 'com.github.piasy:BigImageViewer:1.5.7'
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.0'
}

ext {
    travisBuild = System.getenv("TRAVIS") == "true"
    preDexEnabled = System.getProperty("pre-dex", "true")
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 121
        versionName "1.0.6"
        archivesBaseName += "-$versionName"
        applicationId "me.skriva.ceph"
        resValue "string", "applicationId", applicationId
        resValue "string", "app_name", "Skriva"
        buildConfigField "String", "LOGTAG", "\"skriva\""
    }

    dataBinding {
        enabled true
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
        jumboMode true
        javaMaxHeapSize "12g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions("mode", "distribution", "emoji")

    productFlavors {

        skriva {
            dimension "mode"
        }

        playstore {
            dimension "distribution"
            versionNameSuffix "+p"
        }
        free {
            dimension "distribution"
            versionNameSuffix "+f"
        }
        system {
            dimension "emoji"
            versionNameSuffix "s"
        }
        compat {
            dimension "emoji"
            versionNameSuffix "c"
        }
    }

    sourceSets {
        skrivaFreeCompat {
            java {
                srcDirs 'src/freeCompat/java'
            }
        }
        skrivaPlaystoreCompat {
            java {
                srcDirs 'src/playstoreCompat/java'
            }
            res {
                srcDir 'src/playstoreCompat/res'
                srcDir 'src/skrivaPlaystore/res'
            }
        }
        skrivaPlaystoreSystem {
            res {
                srcDir 'src/skrivaPlaystore/res'
            }
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            versionNameSuffix "r"
        }
        debug {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            versionNameSuffix "d"
        }
    }

    if (new File("signing.properties").exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file("signing.properties")))

        signingConfigs {
            release {
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
                keyAlias props['keystore.alias']
                keyPassword props['keystore.password']
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    }

    lintOptions {
        disable 'ExtraTranslation', 'MissingTranslation', 'InvalidPackage', 'MissingQuantity', 'AppCompatResource'
    }

    subprojects {

        afterEvaluate {
            if (getPlugins().hasPlugin('android') ||
                    getPlugins().hasPlugin('android-library')) {

                configure(android.lintOptions) {
                    disable 'AndroidGradlePluginVersion', 'MissingTranslation'
                }
            }

        }
    }

    packagingOptions {
        exclude 'META-INF/BCKEY.DSA'
        exclude 'META-INF/BCKEY.SF'
    }
}
